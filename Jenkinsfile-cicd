pipeline {
    environment {
        PRJ="hello-${env.BRANCH_NAME}-${env.BUILD_NUMBER}"
        APP='nodeapp'
    }
    agent {
      node {
        label 'nodejs'
      }
    }
    stages {
        stage('create') {
            steps {
                script {
                    //openshift.logLevel(1)
                    openshift.withCluster() {
                        echo("Create project ${env.PRJ}") 
                        openshift.newProject("${env.PRJ}")
                        openshift.withProject("${env.PRJ}") {
                            echo("Create application ${env.APP}") 
                            openshift.newApp('https://github.com/flozanorht/node-cicd.git', "--name ${env.APP}")
                            echo("Expose route for service ${env.APP}") 
                            openshift.expose("svc/${env.APP}")
                        }
                    }
                }
            }
        }
        stage('build') {
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject("${env.PRJ}") {
                            def bc = openshift.selector('bc', "${env.APP}")
                            echo("Wait build from bc ${env.APP} to finish") 
                            timeout(5) {
                                def builds = bc.related('builds').untilEach(1) {
                                    def phase = it.object().status.phase
                                    if (phase == "Failed" || phase == "Error" || phase == "Cancelled") {
                                        error 'OpenShift build failed or was cancelled'
                                    }
                                    return (phase == "Complete")
                                }
                            }
                        }
                    }
                }
            }
        }
        stage('deploy') {
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject("${env.PRJ}") {
                            echo("Wait deployment from dc ${env.APP} to finish") 
                            timeout(5) {
                                openshift.selector('dc', "${env.APP}").rollout().status()
                                openshift.selector('dc', "${env.APP}").related('pods').untilEach(1) {
                                    return (it.object().status.phase == "Running")
                                }
                            }
                        }
                    }
                }
            }
        }
        stage('test') {
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject("${env.PRJ}") {
                            def host = openshift.selector('route', "${env.APP}").object.host
                            echo("Check that app returns HTTP 200") 
                            sh("curl --fail ${host}")
                        }
                    }
                }
            }
        }
    }
    post {
        always {
            script {
                openshift.withCluster() {
                    openshift.delete("project/${env.PRJ}")
                }
            }
        }
    }
}
